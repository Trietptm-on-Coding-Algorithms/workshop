# $HOME/.zshrc

# load gabrielelana/awesome-terminal-fonts
source $HOME/.fonts/fontawesome-regular.sh
source $HOME/.fonts/octicons-regular.sh

function git_branch_char {
  GIT_BRANCH_NAME=$(git branch | awk '/^\*/ {print $2}')
  if [[ "$GIT_BRANCH_NAME" == "master" ]]; then
    echo "\u${CODEPOINT_OF_OCTICONS_STAR} "
  elif [[ "$GIT_BRANCH_NAME" == "dev" ]]; then
    echo "\u${CODEPOINT_OF_OCTICONS_GEAR} "
  elif [[ "$GIT_BRANCH_NAME" == "upstream-master" ]]; then
    echo "\u${CODEPOINT_OF_OCTICONS_ROCKET} "
  elif [[ "$GIT_BRANCH_NAME" == pr/* ]]; then
    echo "$GIT_BRANCH_NAME" | sed "s|pr\/|$(echo -ne \\u$CODEPOINT_OF_OCTICONS_GIT_PULL_REQUEST) |"
  else
    echo "$GIT_BRANCH_NAME"
  fi
}

function git_prompt_char {
  if git remote --verbose | grep "origin.\\+gist\.github.com" &> /dev/null; then
    echo "\u${CODEPOINT_OF_OCTICONS_GIST}"
  elif git remote --verbose | grep "origin.\\+github.com" &> /dev/null; then
    echo "\u${CODEPOINT_OF_AWESOME_GITHUB}"
  else
    echo 'g'
  fi
}

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git hg
zstyle ':vcs_info:*' check-for-changes true
zstyle -e ':vcs_info:git*' formats 'reply=("%{$bg[black]%}%{$fg[white]%} $(git_prompt_char) %u:%c %{$fg_bold[white]%}%r%{$fg_no_bold[white]%}:$(git_branch_char)%{$reset_color%}")'
zstyle ':vcs_info:git*' stagedstr '+'
zstyle ':vcs_info:git*' unstagedstr '!'
zstyle ':vcs_info:hg*' formats '[h %r:%b]'

precmd() {
  vcs_info
}

setopt prompt_subst
VCSPROMPT='${vcs_info_msg_0_}'
PROMPT="$VCSPROMPT$PROMPT"

[[ -d $HOME/.bin ]] && PATH=$PATH:$HOME/.bin
[[ -f $HOME/.zshrc-local ]] && source $HOME/.zshrc-local

export PATH="/usr/local/p/versions/python:$PATH"
